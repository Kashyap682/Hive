---
doctype: Daily
date: 2024-01-09
tags:
  - ESP32
  - ESP32S3
  - TFT
  - Code
---
#### Task Backlog:


#### Tasks:

- [x] Interface ESP32S3 with TFT Panel.
- [x] Display images in 480x320 TFT Panel using example code.
- [x] Display custom images in 480x320 TFT Panel.
#### Code:

``` cpp
#include <SPI.h>
#include <TFT_eSPI.h>

TFT_eSPI tft = TFT_eSPI();

#include <JPEGDecoder.h>

#define minimum(a,b)     (((a) < (b)) ? (a) : (b))

#include "example2.h"  // Assuming jpeg5.h contains the image array

uint32_t icount = 0;

void setup() {
  Serial.begin(115200);
  tft.begin();
}

void loop() {
  tft.setRotation(2);  // portrait
  tft.fillScreen(random(0xFFFF));

  int x = (tft.width()  - 300) / 2 - 1;
  int y = (tft.height() - 300) / 2 - 1;

  drawArrayJpeg(example2, sizeof(example2), x, y); // Draw jpeg5.h image stored in memory at x,y
  delay(2000);
}

void drawArrayJpeg(const uint8_t arrayname[], uint32_t array_size, int xpos, int ypos) {
  int x = xpos;
  int y = ypos;

  JpegDec.decodeArray(arrayname, array_size);

  jpegInfo();
  
  renderJPEG(x, y);
  
  Serial.println("#########################");
}

void renderJPEG(int xpos, int ypos) {
  uint16_t *pImg;
  uint16_t mcu_w = JpegDec.MCUWidth;
  uint16_t mcu_h = JpegDec.MCUHeight;
  uint32_t max_x = JpegDec.width;
  uint32_t max_y = JpegDec.height;

  uint32_t min_w = minimum(mcu_w, max_x % mcu_w);
  uint32_t min_h = minimum(mcu_h, max_y % mcu_h);

  uint32_t win_w = mcu_w;
  uint32_t win_h = mcu_h;

  uint32_t drawTime = millis();

  max_x += xpos;
  max_y += ypos;

  while (JpegDec.read()) {
    pImg = JpegDec.pImage ;

    int mcu_x = JpegDec.MCUx * mcu_w + xpos;
    int mcu_y = JpegDec.MCUy * mcu_h + ypos;

    if (mcu_x + mcu_w <= max_x) win_w = mcu_w;
    else win_w = min_w;

    if (mcu_y + mcu_h <= max_y) win_h = mcu_h;
    else win_h = min_h;

    if (win_w != mcu_w) {
      uint16_t *cImg;
      int p = 0;
      cImg = pImg + win_w;
      for (int h = 1; h < win_h; h++) {
        p += mcu_w;
        for (int w = 0; w < win_w; w++) {
          *cImg = *(pImg + w + p);
          cImg++;
        }
      }
    }

    uint32_t mcu_pixels = win_w * win_h;

    tft.startWrite();

    if (( mcu_x + win_w ) <= tft.width() && ( mcu_y + win_h ) <= tft.height()) {
      tft.setAddrWindow(mcu_x, mcu_y, win_w, win_h);

      while (mcu_pixels--) {
        tft.pushColor(*pImg++);
      }
    }
    else if ( (mcu_y + win_h) >= tft.height()) JpegDec.abort();

    tft.endWrite();
  }

  drawTime = millis() - drawTime;

  Serial.print(F(  "Total render time was    : ")); Serial.print(drawTime); Serial.println(F(" ms"));
  Serial.println(F(""));
}

void jpegInfo() {
  Serial.println(F("==============="));
  Serial.println(F("JPEG image info"));
  Serial.println(F("==============="));
  Serial.print(F(  "Width      :")); Serial.println(JpegDec.width);
  Serial.print(F(  "Height     :")); Serial.println(JpegDec.height);
  Serial.print(F(  "Components :")); Serial.println(JpegDec.comps);
  Serial.print(F(  "MCU / row  :")); Serial.println(JpegDec.MCUSPerRow);
  Serial.print(F(  "MCU / col  :")); Serial.println(JpegDec.MCUSPerCol);
  Serial.print(F(  "Scan type  :")); Serial.println(JpegDec.scanType);
  Serial.print(F(  "MCU width  :")); Serial.println(JpegDec.MCUWidth);
  Serial.print(F(  "MCU height :")); Serial.println(JpegDec.MCUHeight);
  Serial.println(F("==============="));
}

```
#### Outcomes:

- The interfacing was successful.

![[Schematic_ESP32TFT.png|500]]

- Both the example images and custom images were displayed on the 480x320 TFT Panel using ESP32S3 and 3.5 TFT SPI 480x320 V 1.0 Display.
#### References:

- [Image to C Array.](https://notisrac.github.io/FileToCArray/)
- [EasyEDA Schematic Diagram Website.](https://easyeda.com/)